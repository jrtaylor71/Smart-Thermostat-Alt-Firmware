#include <WiFi.h>
#include <ESPAsyncWebServer.h>
#include <Preferences.h>
#include <Wire.h>
#include <DHT.h>
#include <TFT_eSPI.h>
#include <HTTPClient.h>
#include <PubSubClient.h> // Include the MQTT library
#include <esp_task_wdt.h> // Include the watchdog timer library
#include <time.h>

// Constants
const int SECONDS_PER_HOUR = 3600;
const int WDT_TIMEOUT = 10; // Watchdog timer timeout in seconds
#define DHTPIN 22 // Define the pin where the DHT11 is connected
#define DHTTYPE DHT11 // Define the type of DHT sensor

// Globals
DHT dht(DHTPIN, DHTTYPE);
AsyncWebServer server(80);
TFT_eSPI tft = TFT_eSPI();
WiFiClient espClient;
PubSubClient mqttClient(espClient); // Initialize the MQTT client
Preferences preferences; // Preferences instance
String inputText = "";
bool isEnteringSSID = true;

// GPIO pins for relays
const int heatRelay1Pin = 13;
const int heatRelay2Pin = 12;
const int coolRelay1Pin = 14;
const int coolRelay2Pin = 26;
const int fanRelayPin = 25;

// Settings
float setTemp = 72.0; // Default set temperature in Fahrenheit
float tempSwing = 1.0;
bool autoChangeover = false;
bool fanRelayNeeded = false;
bool useFahrenheit = true; // Default to Fahrenheit
bool mqttEnabled = false; // Default to MQTT disabled
bool homeAssistantEnabled = false; // Default to Home Assistant enabled
String location = "54762"; // Default ZIP code
String wifiSSID = "";
String wifiPassword = "";
int fanMinutesPerHour = 15;                                                                     // Default to 15 minutes per hour
unsigned long lastFanRunTime = 0;                                                               // Time when the fan last ran
unsigned long fanRunDuration = 0;                                                               // Duration for which the fan has run in the current hour
String homeAssistantUrl = "http://homeassistant.local:8123/api/states/sensor.esp32_thermostat"; // Replace with your Home Assistant URL
String homeAssistantApiKey = "";                                                                // Home Assistant API Key
int screenBlankTime = 120;                                                                      // Default screen blank time in seconds
unsigned long lastInteractionTime = 0;                                                          // Last interaction time

// MQTT settings
String mqttServer = "192.168.183.238"; // Replace with your MQTT server
int mqttPort = 1883;                    // Replace with your MQTT port
String mqttUsername = "your_username";  // Replace with your MQTT username
String mqttPassword = "your_password";  // Replace with your MQTT password

bool heatingOn = false;
bool coolingOn = false;
bool fanOn = false;
String thermostatMode = "off"; // Default thermostat mode
String fanMode = "auto"; // Default fan mode

// Function prototypes
void setupWiFi();
void controlRelays(float currentTemp);
void handleWebRequests();
void updateDisplay(float currentTemp, float currentHumidity);
void saveSettings();
void loadSettings();
void sendDataToHomeAssistant(float temperature, float humidity);
void setupMQTT();
void reconnectMQTT();
float convertCtoF(float celsius);
void controlFanSchedule();
void saveWiFiSettings();
void drawKeyboard(bool isUpperCaseKeyboard);
void handleKeyPress(int row, int col);
void drawButtons();
void handleButtonPress(uint16_t x, uint16_t y);
void handleKeyboardTouch(uint16_t x, uint16_t y, bool isUpperCaseKeyboard);
void connectToWiFi();
void enterWiFiCredentials();
void calibrateTouchScreen();

uint16_t calibrationData[5] = { 300, 3700, 300, 3700, 7 }; // Example calibration data

float currentTemp = 0.0;
float currentHumidity = 0.0;
bool isUpperCaseKeyboard = true;
float previousTemp = 0.0;
float previousHumidity = 0.0;
float previousSetTemp = 0.0;
bool firstHourAfterBoot = true; // Flag to track the first hour after bootup

void setup()
{
    Serial.begin(115200);

    // Initialize Preferences
    preferences.begin("thermostat", false);
    loadSettings();

    // Debug print to check if homeAssistantEnabled is loaded correctly
    // Serial.print("Home Assistant Enabled: ");
    // Serial.println(homeAssistantEnabled);

    // Initialize the DHT11 sensor
    dht.begin();

    // Initialize the TFT display
    tft.init();
    tft.setRotation(1); // Set the rotation of the display as needed
    tft.fillScreen(TFT_BLACK);
    tft.setTextColor(TFT_WHITE, TFT_BLACK);
    tft.setTextSize(2);
    tft.setCursor(0, 0);
    tft.println("Thermostat");

    // Calibrate touch screen
    calibrateTouchScreen();

    // Initialize relay pins
    pinMode(heatRelay1Pin, OUTPUT);
    pinMode(heatRelay2Pin, OUTPUT);
    pinMode(coolRelay1Pin, OUTPUT);
    pinMode(coolRelay2Pin, OUTPUT);
    pinMode(fanRelayPin, OUTPUT);

    // Ensure all relays are off during bootup
    digitalWrite(heatRelay1Pin, LOW);
    digitalWrite(heatRelay2Pin, LOW);
    digitalWrite(coolRelay1Pin, LOW);
    digitalWrite(coolRelay2Pin, LOW);
    digitalWrite(fanRelayPin, LOW);

    // Setup WiFi
    setupWiFi();

    // Start the web server
    handleWebRequests();
    server.begin();

    if (mqttEnabled) {
        setupMQTT();
    }
    lastInteractionTime = millis();

    // Initialize buttons
    drawButtons();

    // Initialize time
    configTime(0, 0, "pool.ntp.org", "time.nist.gov");

    // Add a delay before attempting to connect to WiFi
    delay(5000);

    // Initial display update
    updateDisplay(currentTemp, currentHumidity);
}

void loop()
{
    static unsigned long lastWiFiAttemptTime = 0;
    static unsigned long lastMQTTAttemptTime = 0;
    static unsigned long lastDisplayUpdateTime = 0;
    const unsigned long displayUpdateInterval = 1000; // Update display every 

    // Feed the watchdog timer
    esp_task_wdt_reset();

    // Attempt to connect to WiFi if not connected
    if (WiFi.status() != WL_CONNECTED && millis() - lastWiFiAttemptTime > 10000)
    {
        connectToWiFi();
        lastWiFiAttemptTime = millis();
    }

    // Comment out MQTT-related code
    /*
    if (mqttEnabled)
    {
        // Attempt to reconnect to MQTT if not connected and WiFi is connected
        if (WiFi.status() == WL_CONNECTED && !mqttClient.connected() && millis() - lastMQTTAttemptTime > 5000)
        {
            reconnectMQTT();
            lastMQTTAttemptTime = millis();
        }
        mqttClient.loop();
    }
    */

    // Read sensor data if sensor is available
    currentTemp = dht.readTemperature(useFahrenheit);
    currentHumidity = dht.readHumidity();

    if (!isnan(currentTemp) && !isnan(currentHumidity))
    {
        // Control relays based on current temperature
        controlRelays(currentTemp);

        // Send data to Home Assistant
        sendDataToHomeAssistant(currentTemp, currentHumidity);
    }

    // Control fan based on schedule
    controlFanSchedule();

    // Handle button presses
    uint16_t x, y;
    if (tft.getTouch(&x, &y))
    {
        Serial.print("Touch detected at: ");
        Serial.print(x);
        Serial.print(", ");
        Serial.println(y);
        handleButtonPress(x, y);
        handleKeyboardTouch(x, y, isUpperCaseKeyboard);
    }

    // Update display periodically
    if (millis() - lastDisplayUpdateTime > displayUpdateInterval)
    {
        updateDisplay(currentTemp, currentHumidity);
        lastDisplayUpdateTime = millis();
    }

    // Control relays based on current temperature
    controlRelays(currentTemp);

    // delay(50); // Adjust delay as needed
}

void setupWiFi()
{
    wifiSSID = preferences.getString("wifiSSID", "");
    wifiPassword = preferences.getString("wifiPassword", "");

    if (wifiSSID != "" && wifiPassword != "")
    {
        WiFi.begin(wifiSSID.c_str(), wifiPassword.c_str());
        unsigned long startAttemptTime = millis();

        // Only try to connect for 10 seconds
        while (WiFi.status() != WL_CONNECTED && millis() - startAttemptTime < 10000)
        {
            delay(1000);
            Serial.println("Connecting to WiFi...");
        }

        if (WiFi.status() == WL_CONNECTED)
        {
            Serial.println("Connected to WiFi");
        }
        else
        {
            Serial.println("Failed to connect to WiFi");
            enterWiFiCredentials();
        }
    }
    else
    {
        // No WiFi credentials found, prompt user to enter them via touch screen
        Serial.println("No WiFi credentials found. Please enter them via the touch screen.");
        enterWiFiCredentials();
    }
}

void enterWiFiCredentials()
{
    drawKeyboard(isUpperCaseKeyboard);
    while (WiFi.status() != WL_CONNECTED)
    {
        uint16_t x, y;
        if (tft.getTouch(&x, &y))
        {
            handleKeyboardTouch(x, y, isUpperCaseKeyboard);
        }
        delay(1000);
        Serial.println("Waiting for WiFi credentials...");
    }
}

void drawKeyboard(bool isUpperCaseKeyboard)
{
    tft.fillScreen(TFT_BLACK);
    tft.setTextColor(TFT_WHITE, TFT_BLACK);
    tft.setTextSize(2);
    tft.setCursor(0, 0);
    tft.println(isEnteringSSID ? "Enter SSID:" : "Enter Password:");
    tft.setCursor(0, 30);
    tft.println(inputText);

    const char* keys[5][10];
    if (isUpperCaseKeyboard)
    {
        const char* upperKeys[5][10] = {
            {"1", "2", "3", "4", "5", "6", "7", "8", "9", "0"},
            {"Q", "W", "E", "R", "T", "Y", "U", "I", "O", "P"},
            {"A", "S", "D", "F", "G", "H", "J", "K", "L", "DEL"},
            {"Z", "X", "C", "V", "B", "N", "M", "SPACE", "CLR", "OK"},
            {"!", "@", "#", "$", "%", "^", "&", "*", "(", "SHIFT"}
        };
        memcpy(keys, upperKeys, sizeof(upperKeys));
    }
    else
    {
        const char* lowerKeys[5][10] = {
            {"1", "2", "3", "4", "5", "6", "7", "8", "9", "0"},
            {"q", "w", "e", "r", "t", "y", "u", "i", "o", "p"},
            {"a", "s", "d", "f", "g", "h", "j", "k", "l", "DEL"},
            {"z", "x", "c", "v", "b", "n", "m", "SPACE", "CLR", "OK"},
            {"!", "@", "#", "$", "%", "^", "&", "*", "(", "SHIFT"}
        };
        memcpy(keys, lowerKeys, sizeof(lowerKeys));
    }

    int keyWidth = 25;  // Reduced by 10%
    int keyHeight = 25; // Reduced by 10%
    int xOffset = 18;   // Adjusted for reduced size
    int yOffset = 81;   // Adjusted for reduced size

    for (int row = 0; row < 5; row++)
    {
        for (int col = 0; col < 10; col++)
        {
            tft.drawRect(col * (keyWidth + 3) + xOffset, row * (keyHeight + 3) + yOffset, keyWidth, keyHeight, TFT_WHITE);
            tft.setCursor(col * (keyWidth + 3) + xOffset + 5, row * (keyHeight + 3) + yOffset + 5);
            tft.print(keys[row][col]);
        }
    }
}

void handleKeyPress(int row, int col)
{
    const char* keys[5][10];
    if (isUpperCaseKeyboard)
    {
        const char* upperKeys[5][10] = {
            {"1", "2", "3", "4", "5", "6", "7", "8", "9", "0"},
            {"Q", "W", "E", "R", "T", "Y", "U", "I", "O", "P"},
            {"A", "S", "D", "F", "G", "H", "J", "K", "L", "DEL"},
            {"Z", "X", "C", "V", "B", "N", "M", "SPACE", "CLR", "OK"},
            {"!", "@", "#", "$", "%", "^", "&", "*", "(", "SHIFT"}
        };
        memcpy(keys, upperKeys, sizeof(upperKeys));
    }
    else
    {
        const char* lowerKeys[5][10] = {
            {"1", "2", "3", "4", "5", "6", "7", "8", "9", "0"},
            {"q", "w", "e", "r", "t", "y", "u", "i", "o", "p"},
            {"a", "s", "d", "f", "g", "h", "j", "k", "l", "DEL"},
            {"z", "x", "c", "v", "b", "n", "m", "SPACE", "CLR", "OK"},
            {"!", "@", "#", "$", "%", "^", "&", "*", "(", "SHIFT"}
        };
        memcpy(keys, lowerKeys, sizeof(lowerKeys));
    }

    const char* keyLabel = keys[row][col];

    if (strcmp(keyLabel, "DEL") == 0)
    {
        if (inputText.length() > 0)
        {
            inputText.remove(inputText.length() - 1);
        }
    }
    else if (strcmp(keyLabel, "SPACE") == 0)
    {
        inputText += " ";
    }
    else if (strcmp(keyLabel, "CLR") == 0)
    {
        inputText = "";
    }
    else if (strcmp(keyLabel, "OK") == 0)
    {
        if (isEnteringSSID)
        {
            wifiSSID = inputText;
            inputText = "";
            isEnteringSSID = false;
            drawKeyboard(isUpperCaseKeyboard);
        }
        else
        {
            wifiPassword = inputText;
            saveWiFiSettings();
            WiFi.begin(wifiSSID.c_str(), wifiPassword.c_str());
            unsigned long startAttemptTime = millis();

            // Only try to connect for 10 seconds
            while (WiFi.status() != WL_CONNECTED && millis() - startAttemptTime < 10000)
            {
                delay(1000);
                Serial.println("Connecting to WiFi...");
            }

            if (WiFi.status() == WL_CONNECTED)
            {
                Serial.println("Connected to WiFi");
            }
            else
            {
                Serial.println("Failed to connect to WiFi");
            }
        }
    }
    else if (strcmp(keyLabel, "SHIFT") == 0)
    {
        isUpperCaseKeyboard = !isUpperCaseKeyboard;
        drawKeyboard(isUpperCaseKeyboard);
    }
    else
    {
        inputText += keyLabel;
    }

    tft.fillRect(0, 30, 320, 30, TFT_BLACK);
    tft.setCursor(0, 30);
    tft.println(inputText);
}

void drawButtons()
{
    // Draw the "+" button
    tft.fillRect(270, 200, 40, 40, TFT_GREEN);
    tft.setCursor(285, 215);
    tft.setTextColor(TFT_WHITE);
    tft.setTextSize(2);
    tft.print("+");

    // Draw the "-" button
    tft.fillRect(50, 200, 40, 40, TFT_RED);
    tft.setCursor(65, 215);
    tft.setTextColor(TFT_WHITE);
    tft.setTextSize(2);
    tft.print("-");

    // Draw the thermostat mode button
    tft.fillRect(130, 200, 60, 40, TFT_BLUE);
    tft.setCursor(140, 215);
    tft.setTextColor(TFT_WHITE);
    tft.setTextSize(2);
    tft.print(thermostatMode);

    // Draw the fan mode button
    tft.fillRect(200, 200, 60, 40, TFT_ORANGE);
    tft.setCursor(210, 215);
    tft.setTextColor(TFT_WHITE);
    tft.setTextSize(2);
    tft.print(fanMode);

    // Add the word "FAN" above the fan mode button
    tft.setCursor(210, 180); // Adjusted y-coordinate to move the text up
    tft.setTextColor(TFT_WHITE);
    tft.setTextSize(2);
    tft.print("FAN");

    // Add the word "MODE" above the mode button
    tft.setCursor(140, 180); // Adjusted y-coordinate to move the text up
    tft.setTextColor(TFT_WHITE);
    tft.setTextSize(2);
    tft.print("MODE");
}

void handleButtonPress(uint16_t x, uint16_t y)
{
    if (x > 270 && x < 310 && y > 200 && y < 240)
    {
        setTemp += 1.0;
        saveSettings();
        updateDisplay(currentTemp, currentHumidity);
    }
    else if (x > 50 && x < 90 && y > 200 && y < 240)
    {
        setTemp -= 1.0;
        saveSettings();
        updateDisplay(currentTemp, currentHumidity);
    }
    else if (x > 130 && x < 190 && y > 200 && y < 240)
    {
        // Change thermostat mode
        if (thermostatMode == "auto")
            thermostatMode = "heat";
        else if (thermostatMode == "heat")
            thermostatMode = "cool";
        else if (thermostatMode == "cool")
            thermostatMode = "off";
        else
            thermostatMode = "auto";

        saveSettings();
        updateDisplay(currentTemp, currentHumidity);
    }
    else if (x > 200 && x < 260 && y > 200 && y < 240)
    {
        // Change fan mode
        if (fanMode == "auto")
            fanMode = "on";
        else
            fanMode = "auto";

        saveSettings();
        updateDisplay(currentTemp, currentHumidity);
    }
}

void setupMQTT()
{
    mqttClient.setServer(mqttServer.c_str(), mqttPort);
}

void reconnectMQTT()
{
    while (!mqttClient.connected())
    {
        Serial.print("Attempting MQTT connection...");
        if (mqttClient.connect("ESP32Thermostat", mqttUsername.c_str(), mqttPassword.c_str()))
        {
            Serial.println("connected");
            // Subscribe to topics or publish messages here
        }
        else
        {
            Serial.print("failed, rc=");
            Serial.print(mqttClient.state());
            Serial.println(" try again in 5 seconds");
            delay(5000);
        }
    }
}

void controlRelays(float currentTemp)
{
    if (thermostatMode == "off")
    {
        // Turn off all relays
        digitalWrite(heatRelay1Pin, LOW);
        digitalWrite(heatRelay2Pin, LOW);
        digitalWrite(coolRelay1Pin, LOW);
        digitalWrite(coolRelay2Pin, LOW);
        digitalWrite(fanRelayPin, LOW);
        heatingOn = false;
        coolingOn = false;
        fanOn = false;
    }
    else if (autoChangeover)
    {
        // Implement auto changeover logic
    }
    else
    {
        // Heating logic
        if (currentTemp < setTemp - tempSwing)
        {
            digitalWrite(heatRelay1Pin, HIGH); // First stage heat
            heatingOn = true;
            if (fanRelayNeeded)
                digitalWrite(fanRelayPin, HIGH);
            if (currentTemp < setTemp - tempSwing - 1.0)
            {
                digitalWrite(heatRelay2Pin, HIGH); // Second stage heat
            }
            else
            {
                digitalWrite(heatRelay2Pin, LOW);
            }
        }
        else
        {
            digitalWrite(heatRelay1Pin, LOW);
            digitalWrite(heatRelay2Pin, LOW);
            heatingOn = false;
        }

        // Cooling logic
        if (currentTemp > setTemp + tempSwing)
        {
            digitalWrite(coolRelay1Pin, HIGH); // First stage cool
            coolingOn = true;
            if (fanRelayNeeded)
                digitalWrite(fanRelayPin, HIGH);
            if (currentTemp > setTemp + tempSwing + 1.0)
            {
                digitalWrite(coolRelay2Pin, HIGH); // Second stage cool
            }
            else
            {
                digitalWrite(coolRelay2Pin, LOW);
            }
        }
        else
        {
            digitalWrite(coolRelay1Pin, LOW);
            digitalWrite(coolRelay2Pin, LOW);
            coolingOn = false;
        }
    }

    // Fan logic
    if (fanMode == "on")
    {
        digitalWrite(fanRelayPin, HIGH); // Turn on fan
        fanOn = true;
    }
    else if (fanMode == "auto")
    {
        if (!heatingOn && !coolingOn)
        {
            digitalWrite(fanRelayPin, LOW); // Turn off fan if not needed
            fanOn = false;
        }
    }    
}

void controlFanSchedule()
{
    if (firstHourAfterBoot)
    {
        unsigned long currentTime = millis();
        if (currentTime >= SECONDS_PER_HOUR * 1000) // Check if an hour has passed
        {
            firstHourAfterBoot = false; // Reset the flag after the first hour
        }
        return; // Skip fan schedule during the first hour
    }

    if (fanMode == "auto")
    {
        unsigned long currentTime = millis();
        unsigned long elapsedTime = (currentTime - lastFanRunTime) / 1000; // Convert to seconds

        // Calculate the duration in seconds the fan should run per hour
        unsigned long fanRunSecondsPerHour = fanMinutesPerHour * 60;

        // If an hour has passed, reset the fan run duration
        if (elapsedTime >= SECONDS_PER_HOUR)
        {
            lastFanRunTime = currentTime;
            fanRunDuration = 0;
        }

        // Run the fan based on the schedule
        if (fanRunDuration < fanRunSecondsPerHour)
        {
            digitalWrite(fanRelayPin, HIGH);
            fanOn = true;
        }
        else
        {
            digitalWrite(fanRelayPin, LOW);
            fanOn = false;
        }

        // Update the fan run duration
        if (fanOn)
        {
            fanRunDuration += elapsedTime;
        }
    }
}

void handleWebRequests()
{
    server.on("/", HTTP_GET, [](AsyncWebServerRequest *request)
              {
        String html = "<html><body>";
        html += "<h1>Thermostat Settings</h1>";
        html += "<form action='/set' method='POST'>";
        html += "Set Temp: <input type='text' name='setTemp' value='" + String(setTemp) + "'><br>";
        html += "Temp Swing: <input type='text' name='tempSwing' value='" + String(tempSwing) + "'><br>";
        html += "Auto Changeover: <input type='checkbox' name='autoChangeover' " + String(autoChangeover ? "checked" : "") + "><br>";
        html += "Fan Relay Needed: <input type='checkbox' name='fanRelayNeeded' " + String(fanRelayNeeded ? "checked" : "") + "><br>"; // Ensure fanRelayNeeded is displayed correctly
        html += "Use Fahrenheit: <input type='checkbox' name='useFahrenheit' " + String(useFahrenheit ? "checked" : "") + "><br>";
        html += "MQTT Enabled: <input type='checkbox' name='mqttEnabled' " + String(mqttEnabled ? "checked" : "") + "><br>";
        html += "Home Assistant Enabled: <input type='checkbox' name='homeAssistantEnabled' " + String(homeAssistantEnabled ? "checked" : "") + "><br>";
        html += "Fan Minutes Per Hour: <input type='text' name='fanMinutesPerHour' value='" + String(fanMinutesPerHour) + "'><br>";
        html += "Location (ZIP): <input type='text' name='location' value='" + location + "'><br>";
        html += "Home Assistant API Key: <input type='text' name='homeAssistantApiKey' value='" + homeAssistantApiKey + "'><br>";
        html += "Screen Blank Time (seconds): <input type='text' name='screenBlankTime' value='" + String(screenBlankTime) + "'><br>";
        html += "<input type='submit' value='Save Settings'>";
        html += "</form>";
        html += "</body></html>";

        request->send(200, "text/html", html); });

    server.on("/set", HTTP_POST, [](AsyncWebServerRequest *request)
              {
        if (request->hasParam("setTemp", true)) {
            setTemp = request->getParam("setTemp", true)->value().toFloat();
        }
        if (request->hasParam("tempSwing", true)) {
            tempSwing = request->getParam("tempSwing", true)->value().toFloat();
        }
        if (request->hasParam("autoChangeover", true)) {
            autoChangeover = request->getParam("autoChangeover", true)->value() == "on";
        }
        if (request->hasParam("fanRelayNeeded", true)) {
            fanRelayNeeded = request->getParam("fanRelayNeeded", true)->value() == "on"; // Ensure fanRelayNeeded is updated correctly
        } else {
            fanRelayNeeded = false; // Ensure fanRelayNeeded is set to false if not present in the form
        }
        if (request->hasParam("useFahrenheit", true)) {
            useFahrenheit = request->getParam("useFahrenheit", true)->value() == "on";
        }
        if (request->hasParam("mqttEnabled", true)) {
            mqttEnabled = request->getParam("mqttEnabled", true)->value() == "on";
        }
        if (request->hasParam("homeAssistantEnabled", true)) {
            homeAssistantEnabled = request->getParam("homeAssistantEnabled", true)->value() == "on";
        }
        if (request->hasParam("fanMinutesPerHour", true)) {
            fanMinutesPerHour = request->getParam("fanMinutesPerHour", true)->value().toInt();
        }
        if (request->hasParam("location", true)) {
            location = request->getParam("location", true)->value();
        }
        if (request->hasParam("homeAssistantApiKey", true)) {
            homeAssistantApiKey = request->getParam("homeAssistantApiKey", true)->value();
        }
        if (request->hasParam("screenBlankTime", true)) {
            screenBlankTime = request->getParam("screenBlankTime", true)->value().toInt();
        }

        saveSettings();
        request->send(200, "text/plain", "Settings saved! Please go back to the previous page."); });

    // Handle RESTful commands from Home Assistant
    server.on("/set_heating", HTTP_POST, [](AsyncWebServerRequest *request)
              {
        if (request->hasParam("heating", true)) {
            String heatingState = request->getParam("heating", true)->value();
            heatingOn = heatingState == "on";
            digitalWrite(heatRelay1Pin, heatingOn ? HIGH : LOW);
            digitalWrite(heatRelay2Pin, heatingOn ? HIGH : LOW);
            request->send(200, "application/json", "{\"heating\": \"" + heatingState + "\"}");
        } else {
            request->send(400, "application/json", "{\"error\": \"Invalid request\"}");
        } });

    server.on("/set_cooling", HTTP_POST, [](AsyncWebServerRequest *request)
              {
        if (request->hasParam("cooling", true)) {
            String coolingState = request->getParam("cooling", true)->value();
            coolingOn = coolingState == "on";
            digitalWrite(coolRelay1Pin, coolingOn ? HIGH : LOW);
            digitalWrite(coolRelay2Pin, coolingOn ? HIGH : LOW);
            request->send(200, "application/json", "{\"cooling\": \"" + coolingState + "\"}");
        } else {
            request->send(400, "application/json", "{\"error\": \"Invalid request\"}");
        } });

    server.on("/set_fan", HTTP_POST, [](AsyncWebServerRequest *request)
              {
        if (request->hasParam("fan", true)) {
            String fanState = request->getParam("fan", true)->value();
            fanOn = fanState == "on";
            digitalWrite(fanRelayPin, fanOn ? HIGH : LOW);
            request->send(200, "application/json", "{\"fan\": \"" + fanState + "\"}");
        } else {
            request->send(400, "application/json", "{\"error\": \"Invalid request\"}");
        } });

    server.on("/temperature", HTTP_GET, [](AsyncWebServerRequest *request)
              {
        float currentTemp = dht.readTemperature(useFahrenheit);
        String response = "{\"temperature\": \"" + String(currentTemp) + "\"}";
        request->send(200, "application/json", response); });

    server.on("/humidity", HTTP_GET, [](AsyncWebServerRequest *request)
              {
        float currentHumidity = dht.readHumidity();
        String response = "{\"humidity\": \"" + String(currentHumidity) + "\"}";
        request->send(200, "application/json", response); });
}

void updateDisplay(float currentTemp, float currentHumidity)
{
    // Update temperature and humidity only if they have changed
    if (currentTemp != previousTemp || currentHumidity != previousHumidity)
    {
        // Clear only the areas that need to be updated
        tft.fillRect(240, 20, 80, 40, TFT_BLACK); // Clear temperature area
        tft.fillRect(240, 60, 80, 40, TFT_BLACK); // Clear humidity area

        // Display temperature and humidity on the right side vertically
        tft.setTextSize(2); // Adjust text size to fit the display
        tft.setRotation(1); // Set rotation for vertical display
        tft.setCursor(240, 20); // Adjust cursor position for temperature
        char tempStr[6];
        dtostrf(currentTemp, 4, 1, tempStr); // Convert temperature to string with 1 decimal place
        tft.print(tempStr);
        tft.println(useFahrenheit ? "F" : "C");

        tft.setCursor(240, 60); // Adjust cursor position for humidity
        char humidityStr[6];
        dtostrf(currentHumidity, 4, 1, humidityStr); // Convert humidity to string with 1 decimal place
        tft.print(humidityStr);
        tft.println("%");

        // Update previous values
        previousTemp = currentTemp;
        previousHumidity = currentHumidity;
    }

    // Update set temperature only if it has changed
    if (setTemp != previousSetTemp)
    {
        // Clear only the area that needs to be updated
        tft.fillRect(60, 100, 200, 40, TFT_BLACK); // Clear set temperature area

        // Display set temperature in the center of the display
        tft.setTextSize(4);
        tft.setCursor(60, 100);
        char tempStr[6];
        dtostrf(setTemp, 4, 1, tempStr); // Convert set temperature to string with 1 decimal place
        tft.print(tempStr);
        tft.println(useFahrenheit ? " F" : " C");

        // Update previous value
        previousSetTemp = setTemp;
    }

    // Draw buttons at the bottom
    drawButtons();
}

void saveSettings()
{
    preferences.putFloat("setTemp", setTemp);
    preferences.putFloat("tempSwing", tempSwing);
    preferences.putBool("autoChangeover", autoChangeover);
    preferences.putBool("fanRelayNeeded", fanRelayNeeded);
    preferences.putBool("useFahrenheit", useFahrenheit);
    preferences.putBool("mqttEnabled", mqttEnabled);
    preferences.putBool("homeAssistantEnabled", homeAssistantEnabled);
    preferences.putInt("fanMinutesPerHour", fanMinutesPerHour);
    preferences.putInt("screenBlankTime", screenBlankTime);
    preferences.putString("location", location);
    preferences.putString("homeAssistantApiKey", homeAssistantApiKey);
    preferences.putString("thermostatMode", thermostatMode);
    preferences.putString("fanMode", fanMode);

    saveWiFiSettings();

    // Debug print to confirm settings are saved
    Serial.println("Settings saved.");
}

void loadSettings()
{
    setTemp = preferences.getFloat("setTemp", 72.0);
    tempSwing = preferences.getFloat("tempSwing", 1.0);
    autoChangeover = preferences.getBool("autoChangeover", false);
    fanRelayNeeded = preferences.getBool("fanRelayNeeded", false);
    useFahrenheit = preferences.getBool("useFahrenheit", true);
    mqttEnabled = preferences.getBool("mqttEnabled", false);
    homeAssistantEnabled = preferences.getBool("homeAssistantEnabled", false); // Default to false
    fanMinutesPerHour = preferences.getInt("fanMinutesPerHour", 15);
    screenBlankTime = preferences.getInt("screenBlankTime", 120);
    location = preferences.getString("location", "54762");
    homeAssistantApiKey = preferences.getString("homeAssistantApiKey", "");
    thermostatMode = preferences.getString("thermostatMode", "off");
    fanMode = preferences.getString("fanMode", "auto");

    wifiSSID = preferences.getString("wifiSSID", "");
    wifiPassword = preferences.getString("wifiPassword", "");

    // Debug print to confirm settings are loaded
    Serial.println("Settings loaded.");
}

void sendDataToHomeAssistant(float temperature, float humidity)
{
    if (homeAssistantEnabled && WiFi.status() == WL_CONNECTED)
    {
        HTTPClient http;
        http.begin(homeAssistantUrl);
        http.addHeader("Content-Type", "application/json");
        http.addHeader("Authorization", "Bearer " + homeAssistantApiKey);

        // Debug prints
        Serial.println("Sending data to Home Assistant...");
        Serial.print("URL: ");
        Serial.println(homeAssistantUrl);
        Serial.print("API Key: ");
        Serial.println(homeAssistantApiKey);

        String payload = "{\"state\": \"" + String(temperature) + "\", \"attributes\": {\"humidity\": \"" + String(humidity) + "\"}}";
        int httpResponseCode = http.POST(payload);

        if (httpResponseCode > 0)
        {
            String response = http.getString();
            Serial.println(httpResponseCode);
            Serial.println(response);
        }
        else
        {
            Serial.print("Error on sending POST: ");
            Serial.println(httpResponseCode);
        }

        http.end();
    }
    else
    {
        // Debug print to indicate that Home Assistant is disabled
        // Serial.println("Home Assistant integration is disabled.");
    }
}

float convertCtoF(float celsius)
{
    return celsius * 9.0 / 5.0 + 32.0;
}

void saveWiFiSettings()
{
    preferences.putString("wifiSSID", wifiSSID);
    preferences.putString("wifiPassword", wifiPassword);
}

void connectToWiFi()
{
    if (wifiSSID != "" && wifiPassword != "")
    {
        Serial.print("Connecting to WiFi with SSID: ");
        Serial.println(wifiSSID);
        Serial.print("Password: ");
        Serial.println(wifiPassword);

        WiFi.begin(wifiSSID.c_str(), wifiPassword.c_str());
        unsigned long startAttemptTime = millis();

        // Only try to connect for 10 seconds
        while (WiFi.status() != WL_CONNECTED && millis() - startAttemptTime < 10000)
        {
            delay(1000);
            Serial.println("Connecting to WiFi...");
        }

        if (WiFi.status() == WL_CONNECTED)
        {
            Serial.println("Connected to WiFi");
        }
        else
        {
            Serial.println("Failed to connect to WiFi");
        }
    }
    else
    {
        // Code to accept WiFi credentials from touch screen or web interface.
        Serial.println("No WiFi credentials found. Please enter them via the web interface.");
        drawKeyboard(isUpperCaseKeyboard);
        while (WiFi.status() != WL_CONNECTED)
        {
            delay(1000);
            Serial.println("Waiting for WiFi credentials...");
        }
    }
}

void calibrateTouchScreen()
{
    uint16_t calData[5];
    uint8_t calDataOK = 0;

    // Check if calibration data is stored in Preferences
    if (preferences.getBytesLength("calData") == sizeof(calData))
    {
        preferences.getBytes("calData", calData, sizeof(calData));
        tft.setTouch(calData);
        calDataOK = 1;
    }

    if (calDataOK && tft.getTouchRaw(&calData[0], &calData[1]))
    {
        Serial.println("Touch screen calibration data loaded from Preferences");
    }
    else
    {
        Serial.println("Calibrating touch screen...");
        tft.fillScreen(TFT_BLACK);
        tft.setCursor(20, 0);
        tft.setTextFont(2);
        tft.setTextSize(1);
        tft.setTextColor(TFT_WHITE, TFT_BLACK);

        tft.println("Touch corners as indicated");
        tft.setTextFont(1);
        tft.println();

        tft.calibrateTouch(calData, TFT_WHITE, TFT_RED, 15);

        // Store calibration data in Preferences
        preferences.putBytes("calData", calData, sizeof(calData));
    }
}

void handleKeyboardTouch(uint16_t x, uint16_t y, bool isUpperCaseKeyboard)
{
    for (int row = 0; row < 5; row++)
    {
        for (int col = 0; col < 10; col++)
        {
            int keyWidth = 25;  // Reduced by 10%
            int keyHeight = 25; // Reduced by 10%
            int xOffset = 18;   // Adjusted for reduced size
            int yOffset = 81;   // Adjusted for reduced size

            if (x > col * (keyWidth + 3) + xOffset && x < col * (keyWidth + 3) + xOffset + keyWidth &&
                y > row * (keyHeight + 3) + yOffset && y < row * (keyHeight + 3) + yOffset + keyHeight)
            {
                Serial.print("Key pressed at row: ");
                Serial.print(row);
                Serial.print(", col: ");
                Serial.println(col);
                handleKeyPress(row, col);
                return;
            }
        }
    }
}